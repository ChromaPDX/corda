// This script exists just to allow bootstrapping the gradle plugins if maven central or jcenter are unavailable
// or if you are developing these plugins. See the readme for more information.

buildscript {
    ext.gradle_plugins_version = "0.6.2" // Our version: bump this on release.
    ext.corda_published_version = "0.6.2" // Depend on our existing published publishing plugin.

    repositories {
        mavenLocal()
        jcenter()
    }

    dependencies {
        classpath "net.corda.plugins:publish-utils:$corda_published_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
    }
}

apply plugin: 'maven-publish'
apply plugin: 'net.corda.plugins.publish-utils'

allprojects {
    version "$gradle_plugins_version"
    group 'net.corda.plugins'
}

subprojects {
	task(install, dependsOn: 'publishToMavenLocal')
}

bintrayConfig {
    user = System.getenv('CORDA_BINTRAY_USER')
    key = System.getenv('CORDA_BINTRAY_KEY')
    repo = 'corda'
    org = 'r3'
    licenses = ['Apache-2.0']
    gpgSign = true
    gpgPassphrase = System.getenv('CORDA_BINTRAY_GPG_PASSPHRASE')
    publications = ['cordformation', 'quasar-utils']
}

// Aliasing the publishToMavenLocal for simplicity.
task(install, dependsOn: 'publishToMavenLocal')

