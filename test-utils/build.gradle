apply plugin: 'kotlin'
apply plugin: 'net.corda.plugins.quasar-utils'
apply plugin: 'net.corda.plugins.publish-utils'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'http://oss.sonatype.org/content/repositories/snapshots'
    }
    jcenter()
    maven {
        url 'https://dl.bintray.com/kotlin/exposed'
    }
}

//noinspection GroovyAssignabilityCheck
configurations {

    // we don't want isolated.jar in classPath, since we want to test jar being dynamically loaded as an attachment
    runtime.exclude module: 'isolated'
}

dependencies {
    compile project(':finance')
    compile project(':core')
    compile project(':node')

    // Log4J: logging framework (with SLF4J bindings)
    compile "org.apache.logging.log4j:log4j-slf4j-impl:${log4j_version}"
    compile "org.apache.logging.log4j:log4j-core:${log4j_version}"

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

    compile "com.google.guava:guava:19.0"

    // Force commons logging to version 1.2 to override Artemis, which pulls in 1.1.3 (ARTEMIS-424)
    compile "commons-logging:commons-logging:1.2"

    // Unit testing helpers.
    compile 'junit:junit:4.12'

    // Guava: Google test library (collections test suite)
    compile "com.google.guava:guava-testlib:19.0"

    // OkHTTP: Simple HTTP library.
    compile 'com.squareup.okhttp3:okhttp:3.3.1'
}

bintray {
    user = System.getenv('CORDA_BINTRAY_USER')
    key = System.getenv('CORDA_BINTRAY_KEY')
    publications = ['testutils']
    dryRun = false
    pkg {
        repo = 'corda'
        name = 'test-utils'
        userOrg = 'r3'
        licenses = ['Apache-2.0']

        version {
            gpg {
                sign = true
                passphrase = System.getenv('CORDA_BINTRAY_GPG_PASSPHRASE')
            }
        }
    }
}

publishing {
    publications {
        testutils(MavenPublication) {
            from components.java
            artifactId 'test-utils'

            artifact sourceJar
            artifact javadocJar

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'test-utils'
                    description 'Testing utilities for Corda'
                    url 'https://github.com/corda/corda'
                    scm {
                        url 'https://github.com/corda/corda'
                    }

                    licenses {
                        license {
                            name 'Apache-2.0'
                            url 'https://www.apache.org/licenses/LICENSE-2.0'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'R3'
                            name 'R3'
                            email 'dev@corda.net'
                        }
                    }
                }
            }
        }
    }
}
